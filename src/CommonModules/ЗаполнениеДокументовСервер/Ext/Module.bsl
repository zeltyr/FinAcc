#Область ПрограммныйИнтерфейс

Процедура СкорректироватьДатыНаСервере(ЗНАЧ ДатаОтсчета, ТабличнаяЧастьДокумента, ОбъектМодифицирован) Экспорт
	
	Если ТабличнаяЧастьДокумента.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДатаОтсчетаЛокальная = НачалоМесяца(ДатаОтсчета);
	СекундВДне = 24*60*60;
	ИмяКолонкиСтатьиДвиженияДС = "СтатьяДвиженияДенежныхСредств";
	ИмяКолонкиКорректируемойДаты = "Дата";
	
	СтатьиДвиженияДенежныхСредств = Новый Массив;
	Для Каждого СтрокаТЧ ИЗ ТабличнаячастьДокумента Цикл
		СтатьиДвиженияДенежныхСредств.Добавить(СтрокаТЧ[ИмяКолонкиСтатьиДвиженияДС]);
	КонецЦикла;
	ДниПриходаРасхода = ПолучитьДниПриходаРасходаСтатейДвиженияДенежныхСредств(СтатьиДвиженияДенежныхСредств);
	
	Для Каждого СтрокаТЧ ИЗ ТабличнаяЧастьДокумента Цикл
		
		День = ДниПриходаРасхода.Получить(СтрокаТЧ[ИмяКолонкиСтатьиДвиженияДС]);
		Если День = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если День > 0 Тогда
			День = День - 1;
		КонецЕсли;
		НоваяДата = ДатаОтсчетаЛокальная + День * СекундВДне;
		Если НоваяДата <> СтрокаТЧ[ИмяКолонкиКорректируемойДаты] Тогда
			ОбъектМодифицирован = Истина;
			СтрокаТЧ[ИмяКолонкиКорректируемойДаты] = НоваяДата;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


Функция ПолучитьДниПриходаРасходаСтатейДвиженияДенежныхСредств(СтатьиДвиженияДенежныхСредств)
	
	ДниПриходаРасхода = Новый Соответствие;
	Если СтатьиДвиженияДенежныхСредств.Количество() = 0 Тогда
		Возврат ДниПриходаРасхода;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатьиДвиженияДенежныхСредств.Ссылка КАК Ссылка,
		|	СтатьиДвиженияДенежныхСредств.ДеньПриходаРасхода КАК ДеньПриходаРасхода
		|ИЗ
		|	Справочник.СтатьиДвиженияДенежныхСредств КАК СтатьиДвиженияДенежныхСредств
		|ГДЕ
		|	СтатьиДвиженияДенежныхСредств.Ссылка В(&СтатьиДвиженияДенежныхСредств)";
	
	Запрос.УстановитьПараметр("СтатьиДвиженияДенежныхСредств", СтатьиДвиженияДенежныхСредств);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДниПриходаРасхода.Вставить(Выборка.Ссылка, Выборка.ДеньПриходаРасхода);
	КонецЦикла;
	
	Возврат ДниПриходаРасхода;
	
КонецФункции

#КонецОбласти
