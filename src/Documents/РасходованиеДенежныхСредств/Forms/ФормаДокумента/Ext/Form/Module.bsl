#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьДельтуСуммДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МесяцПриИзменении(Элемент)
	
	МесяцПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти
 
#Область ОбработчикиСобытийЭлементовТаблицыФормыРасходы

&НаКлиенте
Процедура РасходыСуммаПланПриИзменении(Элемент)
	РасходыСуммаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РасходыСуммаФактПриИзменении(Элемент)
	РасходыСуммаФактПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СкорректироватьДаты(Команда)
	
	СкорректироватьДатыНаСервере();
	
КонецПроцедуры
 
&НаКлиенте
Процедура ЗаполнитьЕжедневнымиТратами(Команда)
	ЗаполнитьЕжедневнымиТратамиНаСервере();
КонецПроцедуры
	
&НаКлиенте
Процедура ЗаполнитьВыделенныеСтроки(Команда)
	
	Если Элементы.Расходы.ВыделенныеСтроки.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='Нет выделенных строк для изменения'");
		ВывестиСообщенияНаФорму(ТекстСообщения);
	Иначе
		ЗапроситьНовоеЧисло();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 
&НаСервере
Процедура ЗаполнитьЕжедневнымиТратамиНаСервере()
	
	Объект.Расходы.Очистить();
	СуммаВДень = Константы.РасходыНаПроживаниеВДень.Получить();
	ДатаНачала = НачалоМесяца(Объект.Месяц);
	ДатаЗавершения = КонецМесяца(Объект.Месяц);
	ДатаВТаблицу = ДатаНачала;
	СекундВДне = 24*60*60;
	Пока ДатаВТаблицу <= ДатаЗавершения Цикл
		СтрокаРасхода = ОБъект.Расходы.Добавить();
		СтрокаРасхода.Дата = ДатаВТаблицу;
		СтрокаРасхода.СтатьяДвиженияДенежныхСредств = Справочники.СтатьиДвиженияДенежныхСредств.ЕжедневныеРасходыНаПроживание;
		СтрокаРасхода.СуммаПлан = СуммаВДень;
		ДатаВТаблицу = ДатаВТаблицу + СекундВДне;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура МесяцПриИзмененииНаСервере()
	
	СкорректироватьДатыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура СкорректироватьДатыНаСервере()
	
	ДанныеОбъекта = РеквизитФормыВЗначение("Объект");
	ЗаполнениеДокументовВызовСервера.СкорректироватьДатыНаСервере(Объект.Месяц, ДанныеОбъекта.Расходы, Модифицированность);
	ЗначениеВРеквизитФормы(ДанныеОбъекта, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура РасходыСуммаФактПриИзмененииНаСервере()
	
	Объект.СуммаДокументаФакт = Объект.Расходы.Итог("СуммаФакт");
	ОбновитьДельтуСуммДокумента();
	
КонецПроцедуры

&НаСервере
Процедура РасходыСуммаПриИзмененииНаСервере()
	
	Объект.СуммаДокументаПлан = Объект.Расходы.Итог("СуммаПлан");
	ОбновитьДельтуСуммДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДельтуСуммДокумента()
	
	ДельтаПланФакт = Объект.СуммаДокументаПлан - Объект.СуммаДокументаФакт;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВыделенныеСтрокиНаСервере(НоваяСумма)
	
	СтрокиКОбработке = Элементы.Расходы.ВыделенныеСтроки;
	Для каждого СтрокаКОбработке ИЗ СтрокиКОбработке Цикл
		СтрокаТЧ = Объект.Расходы.НайтиПоИдентификатору(СтрокаКОбработке);
		СтрокаТЧ.СуммаПлан = НоваяСумма;
	КонецЦикла;
	
КонецПроцедуры
 	
&НаКлиенте
АСИНХ Процедура ЗапроситьНовоеЧисло()
	
	НоваяСумма = Ждать ВвестиЧислоАсинх(0, "Укажите новую сумму (план) для выделенных строк", 4, 0);
	Если НоваяСумма <= 0 Тогда
		ТекстСообщения = НСтр("ru='Новая сумма меньше или равна нулю, что недопустимо'");
		ВывестиСообщенияНаФорму(ТекстСообщения);
	Иначе
		ЗаполнитьВыделенныеСтрокиНаСервере(НоваяСумма);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСообщенияНаФорму(ТекстыСообщений)
	
	Если ТипЗнч(ТекстыСообщений) = Тип("Строка") Тогда
		ИнформацияДляВывода = Новый Массив;
		ИнформацияДляВывода.Добавить(ТекстыСообщений);
	Иначе
		ИнформацияДляВывода = ТекстыСообщений;
	КонецЕсли;
	
	Сообщ = Новый СообщениеПользователю;
	Для каждого ТекстСообщения ИЗ ИнформацияДляВывода Цикл
		
		Сообщ.Текст = ТекстСообщения;
		Сообщ.Сообщить();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти 
