#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	СуммаДокументаПлан = Поступления.Итог("СуммаПлан");
	СуммаДокументаФакт = Поступления.Итог("СуммаФакт");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоступленияДенежныхСредствПоступления.Дата КАК Дата,
	|	ПоступленияДенежныхСредствПоступления.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	СУММА(ПоступленияДенежныхСредствПоступления.СуммаПлан) КАК СуммаПлан,
	|	СУММА(ПоступленияДенежныхСредствПоступления.СуммаФакт) КАК СуммаФакт,
	|	ВЫРАЗИТЬ(ПоступленияДенежныхСредствПоступления.Описание КАК СТРОКА(250)) КАК Описание
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	Документ.ПоступленияДенежныхСредств.Поступления КАК ПоступленияДенежныхСредствПоступления
	|ГДЕ
	|	ПоступленияДенежныхСредствПоступления.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПоступленияДенежныхСредствПоступления.Дата,
	|	ПоступленияДенежныхСредствПоступления.СтатьяДвиженияДенежныхСредств,
	|	ВЫРАЗИТЬ(ПоступленияДенежныхСредствПоступления.Описание КАК СТРОКА(250))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.Дата КАК Период,
	|	ВТ_ДанныеДокумента.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ВТ_ДанныеДокумента.СуммаПлан КАК СуммаПриход,
	|	ВТ_ДанныеДокумента.СуммаФакт КАК СуммаФактПриход,
	|	ВТ_ДанныеДокумента.Описание КАК Описание
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ДвиженияДляОтражения = РезультатЗапроса.Выгрузить();
	
	Движения.УчетФинансовОбороты.Записывать = Истина;
	Движения.УчетФинансовОбороты.Загрузить(ДвиженияДляОтражения);
	
КонецПроцедуры

#КонецОбласти

#Иначе
	
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
	
#КонецЕсли

